//Credits goes to Nash, owner of Nashgore

// Base blood
class NashGore_BloodBase : Actor
{
	//const int NASHGORE_BLOODSPOT_DURATION == 1050;
	//const int NASHGORE_BLOODFLAGS1 == SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY;
	//const int NASHGORE_BLOODFLAGS2 == SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION;
	//const int NASHGORE_BLOODFLAGS3 == SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE;
	
	Default
	{
		+CORPSE
		+DONTSPLASH
		+FORCEXYBILLBOARD
		+NOTELEPORT
		+NOBLOCKMONST
		+NOBLOCKMAP
		-SOLID
	}
}

// Emulated regular Doom blood
class NashGore_DoomBlood : Blood
{
	Default
	{
		+DONTSPLASH
		+FORCEXYBILLBOARD
	}
}

// Modify the internal Blood actor to spawn flying blood
class NashGore_Blood : Blood replaces Blood
{
	Default
	{
		-RANDOMIZE
	}
	States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 0 A_SpawnItem("NashGore_DoomBlood", 0, 0, 0, 1);
			TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 0, FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.0, 6.0), 0, SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 64);
			TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 0, FRandom(0.2, 3.0) * RandomPick(1, -1), FRandom(0.2, 3.0) * RandomPick(1, -1), FRandom(0.0, 3.0), 0, SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY, 64);
			Stop;
	}
}

// Blood actor that flies outwards and leaves trails behind
class NashGore_FlyingBlood : NashGore_BloodBase
{
	Default
	{
		Radius 8;
		Height 2;
		Gravity 0.25;
	}
	States
	{
		Spawn:
		FlyingNormal:
			TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1);
			TNT1 A 0 A_Jump(12, "RandomlyDestroy");
			TNT1 A 0 A_Jump(27, "FlyingDecel");
			Loop;
		FlyingDecel:
			TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1);
			TNT1 A 0 A_ChangeVelocity(velx * 0.8, vely * 0.8, velz, CVF_REPLACE);
			Loop;
		RandomlyDestroy:
			TNT1 A 0;
			Stop;
		Crash:
			TNT1 A 0 A_PlaySound("bloodsplat");
			Stop;
	}
}

// Blood trails spawned by FlyingBlood
class NashGore_FlyingBloodTrail : NashGore_BloodBase
{
	Default
	{
		Radius 1;
		Height 1;
		Gravity 0.15;
		Scale 0.55;
	}
	States
	{
		Spawn:
			BTRL ABCD 4; //bright
			Loop;
		Crash:
			TNT1 A 0;
			Stop;
	}
}
