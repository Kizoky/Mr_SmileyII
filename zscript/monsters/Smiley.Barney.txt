//===========================================================================
//
// Pink Demon // Barney | Hugaholic
//
//===========================================================================
class Hugaholic : SmileyActor
{
	bool Transformed, canTransform;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		bool transf = random[transform](0,1);
		
		if (transf)
			canTransform = true;
	}
	
	void A_HugAttack()
	{
		let targ = target;
		if (targ && CheckMeleeRange())
		{
			int damage = random[pr_sargattack](1, 10) * 4;
			int newdam = targ.DamageMobj (self, self, damage, "Melee");
			targ.TraceBleed (newdam > 0 ? newdam : damage, self);
		}
	}
	
	void A_PrepareYourself()
	{
		if (canTransform)
		{
			if (!Transformed)
			{
				if (self.Health < 120)
				{
					A_StartSound("barney/transform", CHAN_VOICE);
					Speed = 20;
					//Radius = 50;
					Height = 67;
					Scale.X = 1.3;
					Scale.Y = Scale.X;
					Health =+ 125;
					Transformed = true;
				}
			}
		}
	}
	
	Default
	{
		Health 150;
		PainChance 180;
		Speed 10;
		Radius 30;
		Height 56;
		Mass 400;
		Monster;
		+FLOORCLIP
		SeeSound "demon/sight";
		AttackSound "demon/melee";
		PainSound "demon/pain";
		DeathSound "demon/death";
		ActiveSound "demon/active";
		Obituary "$OB_DEMONHIT";
	}
	States
	{
	Spawn:
		SARG AB 10 A_Look;
		Loop;
	See:
		SARG AABBCCDD 2 Fast A_Chase;
		Loop;
	Melee:
		SARG EF 8 Fast A_FaceTarget;
		SARG G 8 Fast A_HugAttack;
		Goto See;
	Pain:
		SARG H 0 A_PrepareYourself();
		SARG H 2 Fast;
		SARG H 2 Fast A_Pain;
		Goto See;
	Death:
		SARG I 0 A_Mirrorize();
		SARG I 8;
		SARG J 8 A_Scream;
		SARG K 4;
		SARG L 4 A_NoBlocking;
		SARG M 4;
		SARG N -1;
		Stop;
	Raise:
		SARG N 0 A_Metalization();
		SARG N 0 A_ResurrectSphere();
		SARG N 5;
		SARG MLKJI 13;
		SARG I 0 A_UnMirrorize();
		Goto See;
	}
}

//===========================================================================
//
// Spectre // Silent version of Barney
//
//===========================================================================
class SilentHug : Hugaholic replaces Spectre
{
	Default
	{
		+SHADOW
		RenderStyle "OptFuzzy";
		Alpha 0.5;
		SeeSound "spectre/sight";
		AttackSound "spectre/melee";
		PainSound "spectre/pain";
		DeathSound "spectre/death";
		ActiveSound "spectre/active";
		HitObituary "$OB_SPECTREHIT";
	}
}
