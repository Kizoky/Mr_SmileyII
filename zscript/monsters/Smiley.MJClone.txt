//===========================================================================
//
// Zombie man // Michael Jackson Clone
//
//===========================================================================
class MJClone : SmileyActor
{
	void A_MJAttack()
	{
		if (target)
		{
			A_FaceTarget();
			double ang = angle;
			double slope = AimLineAttack(ang, MISSILERANGE);
			A_StartSound("grunt/attack", CHAN_WEAPON);
			ang  += Random2[PosAttack]() * (22.5/256);
			int damage = Random[PosAttack](1, 5) * 3;
			LineAttack(ang, MISSILERANGE, slope, damage, "Hitscan", "Bulletpuff");
		}
	}
	
	Default
	{
		Health 20;
		Radius 20;
		Height 56;
		Speed 10;
		PainChance 200;
		Monster;
		+FLOORCLIP
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		Obituary "$OB_ZOMBIE";
		DropItem "Clip";
	}
	States
	{
	Spawn:
		POSS AB 10 A_Look;
		Loop;
	See:
		POSS AABBCCDD 3 A_Chase;
		Loop;
	Missile:
		POSS E 10 A_FaceTarget;
		POSS F 0 A_SmoothCriminal;
		POSS F 6 A_MJAttack;
		POSS E 8;
		Goto See;
	Pain:
		POSS G 3;
		POSS G 3 A_Pain;
		Goto See;
	Death:
		POSS H 0 A_Mirrorize();
		POSS H 5;
		POSS I 5 A_Scream;
		POSS J 5 A_NoBlocking;
		POSS K 5;
		POSS L -1;
		Stop;
	XDeath:
		POSS M 0 A_Mirrorize();
		POSS M 5;
		POSS N 5 A_XScream;
		POSS O 5 A_NoBlocking;
		POSS PQRST 5;
		POSS U -1;
		Stop;
	Raise:
		POSS K 0 A_Metalization();
		POSS K 0 A_ResurrectSphere();
		POSS K 5;
		POSS JIH 20;
		POSS H 0 A_UnMirrorize();
		Goto See;
	}
}

extend class MJClone
{
    void A_SmoothCriminal()
	{
		bool LR = random[smooth](0,1);
		int mov = random[moving](5,10);

		if (LR)
			Thrust(mov, Angle - 90);
		else
			Thrust(mov, Angle + 90);
			
		A_StartSound("mjclone/ow", CHAN_VOICE);
				
	}
}